on load:
#    resetrole()
    set {time} to 500 if {time} is not set
function resetrole():
    clear {role::black::*}
    clear {role::white::*}
    clear {role::other::*}
    set {role::black::&c人狼} to 0
    set {role::other::&d狂人} to 0
    set {role::white::&a村人} to 0
    set {role::white::&b予言者} to 0
    set {role::white::&e探偵} to 0
    set {role::other::&1怪盗} to 0
    set {role::white::&dエンチャンター} to 0
function yogen(player: player) :: text:
    loop indices of {role::black::*}:
        {role::black::%loop-value%::*} contains {_player}
        return "%{_player}%は&4&l黒&rです。"
        stop
    return "%{_player}%は白です。"
function getrole(player: player) :: text:
    loop indices of {role::other::*}:
        {role::other::%loop-value%::*} contains {_player}
        return loop-value
        stop
    loop indices of {role::white::*}:
        {role::white::%loop-value%::*} contains {_player}
        return loop-value
        stop
    loop indices of {role::black::*}:
        {role::black::%loop-value%::*} contains {_player}
        return loop-value
        stop

command /start:
    permission: op
    trigger:
        clear {player::*}
        if {ingame} is not set:
            set {ingame} to false
        if {ingame} is true:
            send "試合なぅ" to player
            stop
        {ingame} is false
        clear {nowvote} 
        send title "れみカス人狼スタート!!!" to all players
        command "/playsound minecraft:entity.wolf.howl master @a ~ ~ ~ 1 0 1"
        set {spawn} to location of player
        rungame()
function giverole():
    set {role::black} to 0
    set {role::white} to 0
    give bow to all players
    give arrow to all players
    give redstone torch named "杖" with lore "右クリック" to all players
    give 64 dried kelp to  all players
    set {_c} to 1
    loop shuffled all players:
        add loop-player to {player::*}
        give randomitem() to loop-player
    loop indices of {role::*}:
        loop indices of {role::%loop-value-1%::*}:
            loop {role::%loop-value-1%::%loop-value-2%} time:
                if loop-value-1 is "black":
                    add 1 to {role::black}
                    command "effect give %{player::%{_c}%}% regeneration infinite 0 true"
                    command "effect give %{player::%{_c}%}% night_vision infinite 7 true"
                    command "effect give %{player::%{_c}%}% resistance infinite 0 true"
                if loop-value-1 is "white":
                    add 1 to {role::white}
                if loop-value-2 is "&dエンチャンター":
                    set level of {player::%{_c}%} to 29
                add {player::%{_c}%} to {role::%loop-value-1%::%loop-value-2%::*}
                add 1 to {_c}
    loop all players:
        checkmyrole(loop-player)        
function checkmyrole(player: player):
    send "あなたの役職は%getrole({_player})%&rです" to {_player}
    getrole({_player}) is "&c人狼"
    loop {role::black::&c人狼::*}:
        add loop-value to {_b::*}              
    send "仲間は%{_b::*}%" to {_player}
function rungame():
    loop all players:
        reset(loop-player)
    delete all dropped items
    giverole()
    set {_bar} to boss bar with id "time"
    set bar visibility of {_bar} to true
    set {ingame} to true
    loop {time}*20 time:
        add all players to {_bar}
        set bar progress of {_bar} to 100-((loop-number/20)/{time})*100
        gmend("white") if {role::black} <= 0
        gmend("black") if {role::white} <= 0
        wait tick
        {ingame} is false
        stop
    gmend("time")
every 25 second:
    {ingame} is true
    increase all players's level by 1

function gmend(winner: text):
    set {_bar} to boss bar with id "time"
    clear {player::*}
    set bar visibility of {_bar} to false
    set {ingame} to false
    send title "試合終了!" to all players 
    force teleport all players to {spawn}
    loop all players:
        set metadata value "used" of loop-player to false
        showroles(loop-player)
        reset(loop-player)
    if {_winner} is "time":
        send "&7時間切れによる試合終了" to all players
    else if {_winner} is "force":
        send "&7管理者による強制試合終了" to all players
    else if {_winner} is "white":
        send "&7白陣営の勝利!!!!" to all players
    else if {_winner} is "black":
        send "&7黒陣営の勝利!!!!" to all players
    loop indices of {role::white::*}:
        clear {role::white::%loop-value%::*}
    loop indices of {role::black::*}:
        clear {role::black::%loop-value%::*}
    loop indices of {role::other::*}:
        clear {role::other::%loop-value%::*}
    clear {nowvote} 
    clear {player::spec::*}
command /end <text=force>:
    permission: op
    trigger:
        gmend(arg)
command /setting <text> <number>:
    permission: op
    trigger:
        if arg-1 is "time":
            set {time} to arg-2
        broadcast "set %arg-1% to %arg-2%"
on rightclick on a ender chest:
    cancel event
    player has permission "op"
    rolegui(player)

function rolegui(player: player):
    if {ingame} is true:
        send "役職メニューの表示を停止しました 理由：試合中" to {_player}
        stop
    set {_i} to {_player}'s enderchest 
    set {_count} to 0
    loop all books in {_i}:
        add item amount of loop-item to {_a}
    set {role::white::&a村人} to real online player count - {_a}

    set slot integers from 0 to 36 of {_i} to air
    loop indices of {role::*}:
        loop indices of {role::%loop-value-1%::*}:
            set slot {_count} of {_i} to book named loop-value-2 with lore "%{role::%loop-value-1%::%loop-value-2%}%"
            set slot {_count} of {_i} to barrier named loop-value-2 if {role::%loop-value-1%::%loop-value-2%} <= 0
            if loop-value-2 is "&a村人":
                set slot {_count} of {_i} to paper named loop-value-2 with lore "%{role::%loop-value-1%::%loop-value-2%}%"

            add 1 to {_count}    
    open {_i} to {_player} 
on inventory click:
    set {_i} to player's enderchest 
    event-inventory is {_i}
    cancel event
    event-item is not paper or air
    if inventory action is pickup all:
        loop indices of {role::*}:
            {role::%loop-value%::%name of event-item%} is set
            amount of book in {_i} is smaller than real online player count 
            add 1 to {role::%loop-value%::%name of event-item%}
        rolegui(player)
    else if inventory action is pickup half:
        loop indices of {role::*}:
            {role::%loop-value%::%name of event-item%} is set
            {role::%loop-value%::%name of event-item%} is not smaller than 1
            add -1 to {role::%loop-value%::%name of event-item%}
            rolegui(player)
    rolegui(player)
    
on inventory close:
    set {_i} to player's enderchest 
    event-inventory is {_i}


on rightclick on chest or trapped chest or furnace or barrel or brewing stand or any stair or any sign:
    gamemode of player is not creative
    cancel event
    if event-block is any stair:
        tool of player is not bow
        uncancel event
on projectile hit:
    event-projectile is arrow
    wait 5 tick
    delete event-projectile
on damage of player:
    if victim is attacker:
        cancel event
        stop
    projectile is arrow
    if getrole(attacker) is "&c人狼":
        if tool of victim or offhand tool of victim is totem of undying:
            increase damage by 1
            stop
        increase damage by 3  
    else:
        if tool of victim or offhand tool of victim is totem of undying:
            increase damage by 0.5
            stop
        increase damage by 2

on item damage:
    item is bow
    cancel event
on damage of player:
    health of victim < final damage
    if tool of victim or offhand tool of victim is totem of undying:
        remove 1 totem of undying from victim
        command "/execute as %victim% at @s run playsound minecraft:item.totem.use"
        heal victim by 15
        add potion effect of resistance of tier 5 for 5 seconds to potion effects of victim
        add potion effect of glowing of tier 5 for 5 seconds to potion effects of victim
        cancel event

on death of player:
    force victim to respawn
    {ingame} is true
    set {_p} to victim
    set death message to ""
    cancel event if gamemode of victim is not spectator
    drop victim's skull named "%victim%"
    reset(victim)
    death(victim)
    set gamemode of victim to spectator
    set {player::spec::*} to {_p}
on death of player:
    wait 2 tick
    force teleport victim to {spawn}
command /test:
    permission: op
    trigger:
        showroles(player)
        send {role::white}
        send {role::black}
function showroles(player: player):
    loop all players:
        send "%getrole(loop-player)%: %loop-player%" to {_player}
function death(player: player):
    {_player} do not contain {player::spec::*}
    if getrole({_player}) contains indices of {role::black::*}:
        remove 1 from {role::black}
    else:
        getrole({_player}) is not "&1怪盗" or "&d狂人"
        remove 1 from {role::white} 

on rightclick:
    tool of player is redstone torch
    checkmyrole(player)
    selectplayersgui(player) if getrole(player) is "&b予言者"
function selectplayersgui(p: player):
    set {_gui} to chest inventory with 6 rows named "能力の使用対象を選ぼう！"
    loop {player::*}:
        set slot loop-counter - 1 of {_gui} to loop-value's skull named "%loop-value%"
    open {_gui} to {_p}
on inventory click:
    event-inventory is not player's inventory
    event-slot is not empty
    name of event-inventory is "能力の使用対象を選ぼう！"
    cancel event
    if getrole(player) is "&b予言者":
        metadata value "used" of player is not true
        send yogen(name of event-item parsed as player)
        set metadata value "used" of player to true
every tick:
    set all players's food level to 9.5
on inventory open:
    event-inventory is enchanting table inventory
    set slot 1 of event-inventory to 3 lapis lazuli
on inventory close:
    event-inventory is enchanting table inventory
    set slot 1 of event-inventory to air
on inventory click:
    event-inventory is enchanting table inventory
    event-item is lapis lazuli
    cancel event
on shoot:
    projectile is an arrow
    remove every arrow from shooter
    give arrow to shooter
on place:
    gamemode of player is not creative
    cancel event
on break:
    gamemode of player is not creative
    cancel event
on left click:
    tool of player is not golden hoe 
    item cooldown of arrow for player is 0 seconds
    if getrole(player) is "&c人狼":
        set item cooldown of arrow for player to 1 seconds
    else:
        set item cooldown of arrow for player to 1.25 seconds
    push player forwards

on click with golden hoe:
    tool of player is golden hoe
    item cooldown of golden hoe for player is 0 seconds
    set item cooldown of golden hoe for player to 0.5 seconds
    push player forwards at speed 1
    add potion effect of slow falling of tier 1 for 1 seconds to potion effects of player
    loop players in radius 6 of player:
        loop 5 time:
            force player to attack loop-player
            set no damage tick of loop-player to 0
            wait 1 tick

#random items
function randomitem() :: item:
    set {_i::0} to totem of undying 
    set {_i::1} to potion
    add potion effect of invisibility for 30 seconds to potion effects of {_i::1}
    set {_i::2} to potion named "俊敏+跳躍"
    add potion effect of jump boost of tier 5 for 30 seconds to potion effects of {_i::2}
    add potion effect of speed of tier 14 for 30 seconds to potion effects of {_i::2}
    set {_i::3} to golden hoe named "" 
    set {_i::4} to elytra named "滑空翼" with lore "⚠️激突に注意⚠️"
    set {_i::5} to potion named "透明+再生"
    add potion effect of invisibility for 30 seconds to potion effects of {_i::5}
    add potion effect of regeneration of tier 3 for 30 seconds to potion effects of {_i::5}

    return random element of {_i::*}



command /resetrole:
    permission: op
    trigger:
        resetrole()
on join:
    {ingame} is true
    set gamemode of player to spectator 
    set {player::spec::*} to player
on leave:
    {ingame} is true
    death(player)
on player pick up of player skull:
    if getrole(player) is not "&e探偵":
        cancel event
        stop
        metadata value "used" of player is not true
    set {_target} to event-item's name parsed as player
    send yogen({_target}) to player
    set metadata value "used" of player to true


jinro.sk一番最後の行